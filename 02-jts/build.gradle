apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "net.ltgt.apt"
apply plugin: 'net.ltgt.apt-idea'
apply plugin: "io.freefair.lombok"

group = 'xyz.lonelyleaf'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

//添加mapstuct的一些配置
compileJava {
    options.compilerArgs = [
            '-Amapstruct.defaultComponentModel=spring',//默认生成为spring bean
            '-Amapstruct.unmappedTargetPolicy=ERROR'//未映射字段默认报错
    ]
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    //mybatis
    implementation "com.baomidou:mybatis-plus-boot-starter:$mybatisPlusVersion"
    //数据库管理
    implementation 'org.flywaydb:flyway-core'
    //pg驱动
    implementation "org.postgresql:postgresql:$postgresJdbcVersion"
    //postgis
    implementation 'net.postgis:postgis-jdbc:2.3.0'

    //jackson相关
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-json-org"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    //implementation 'com.fasterxml.jackson.datatype:jackson-datatype-joda'
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names"
    implementation "com.fasterxml.jackson.module:jackson-module-afterburner"

    //java bean转换
    implementation "org.mapstruct:mapstruct-jdk8:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

    //swagger文档
    implementation("io.springfox:springfox-swagger2:$springFoxVersion") {
        exclude group: 'io.swagger', module: 'swagger-annotations'
        exclude group: 'io.swagger', module: 'swagger-models'
    }
    implementation "io.springfox:springfox-swagger-ui:$springFoxVersion"
    //添加下面依赖是为了排除获取api文档时的报错：java.lang.NumberFormatException: For input string: ""
    //后面升级springfox后，可以移除下面的两个依赖
    implementation 'io.swagger:swagger-annotations:1.5.21'
    implementation 'io.swagger:swagger-models:1.5.21'

    //jts
    implementation "org.locationtech.jts:jts-core:$jtsVersion"
    implementation 'org.locationtech.spatial4j:spatial4j:0.7'
    //序列化为geojson
    //这个没维护了，只有自己改下
    //implementation 'com.bedatadriven:jackson-datatype-jts:2.2'
    implementation project(':00-jackson-datatype-jts')
//    implementation 'org.wololo:jts2geojson:0.14.1'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}
